 OLD
>LIST
   10REM  *** Airline ***     
   20REM  By Tony Kingsmill July '92
   30 REM   PUBLIC DOMAIN
   40REM
   50 MODE 4:VDU 19,128,4,0,0,0
   70RESTORE 80:DIM M$(12):FOR X=1 TO 12:READ M$(X):NEXT X
   80DATA Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sep,Oct,Nov,Dec
   90REM * * VARIABLES
  100Y=1992:AL=3:F=150000:M=50000 
  110W=165000:MC=30000:C=950000 
  120FC=90:P=125:TV=300000:R=50000
  130PA=40000:MA=30000:K=0:MT=1
  133 T=5000:T1=0
  135 Q=0
  140PRINT:PRINT"AIRLINE >>>":PRINT
  145 PRINT"(c)1992 TONY KINGSMILL"  
  147 PRINT
  150PRINT"Welcome to Airline! The objective of    this game is to control a civil airline company and to make as much money as    possible. You shall start in January"
  160PRINT"1992 with `50,000 and three aircraft."
  170PRINT:PRINT"Each plane can carry up to 150 people,  but you have to work hard to fill all   the seats. To encourage people to your"
  180PRINT"airline, you can either advertise your  services or reduce prices (or both).    However, beware of running out of money as running costs are very high!"
  190 PRINT:PRINT:PRINT:COLOUR 3:PRINT"PRESS ANY KEY TO COMMENCE.." 
  200I$=GET$
  210 CLS
  220PRINT:PRINT"PLEASE ENTER YOUR NAME:":INPUT N$
  230PRINT:PRINT"ENTER YOUR COMPANY'S NAME:":INPUT A$
  240PRINT:PRINT"Ok, let's fly!":FOR X=1 TO 1000:NEXT X
  250REM * * MAIN MENU
  260CLS:PRINT TAB(17);"AIRLINE"
  270PRINT TAB(17);"*******"
  275 PRINT:PRINT M$(MT);" ";Y 
  280PRINT:PRINT TAB(16);"Main Menu"   
  290 PRINT:PRINT
  300PRINT"   1 .. AIRLINE INFORMATION"
  305 PRINT
  310PRINT"   2 .. BUY AN ADDITIONAL PLANE"
  315 PRINT
  320PRINT"   3 .. SELL ONE OF YOUR PLANES"
  325 PRINT
  330PRINT"   4 .. ADVERTISE YOUR SERVICES"
  335 PRINT
  340PRINT"   5 .. SAVE POSITION TO CASSETTE"
  345 PRINT
  350PRINT"   6 .. LOAD POSITION FROM CASSETTE"
  355 PRINT
  360PRINT"   7 .. CONTINUE MONTH"
  370 PRINT
  380 PRINT:INPUT"   PLEASE SELECT 1-7:  ";Z$     
  390 IF Z$="" THEN GOTO 7000
  400IF Z$="1" THEN GOSUB 1000
  410IF Z$="2" THEN GOSUB 2000
  420IF Z$="3" THEN GOSUB 3000
  430IF Z$="4" THEN GOSUB 4000
  440IF Z$="5" THEN GOSUB 5000
  450IF Z$="6" THEN GOSUB 6000
  460IF Z$="7" THEN GOTO 7000
  470GOTO 260 :REM  BACK TO MENU
 1000 REM * * INFORMATION
 1010 CLS:PRINT:PRINT"AIRLINE INFORMATION >>>":PRINT
 1020 PRINT"Company:";A$:PRINT:PRINT"Chairman:";N$:PRINT  
 1030 PRINT"Finance:";M:PRINT
 1035 PRINT"Profit Target This Year:";T
 1037 PRINT
 1040 PRINT"No. of aircraft:";AL:PRINT 
 1050 PRINT"Average no. of people per plane:";P
 1055 PRINT
 1060 PRINT"Cost per flight:";FC
 1065 PRINT:PRINT:PRINT
 1070 PRINT"PRESS ANY KEY TO CONTINUE.."
 1080 I$=GET$
 1090 RETURN
 2000 REM * * BUY AIRLINE
 2010CLS:PRINT:PRINT"BUY AIRCRAFT >>>"    
 2020PRINT:PRINT"Current cost of new airline:";C
 2030PRINT:PRINT"Current funds:";M
 2040PRINT:PRINT"Present no. of planes:";AL
 2050 PRINT:PRINT
 2055 IF Q=1 THEN PRINT"YOU CANNOT BUY MORE PLANES THIS MONTH!":GOTO 2130
 2060 INPUT"Buy  y/n:  ";X$
 2070IF X$="Y" OR X$="y" THEN 2080 ELSE 2400
 2080PRINT:INPUT"Buy how many planes   ";Q     
 2090IF Q<0 THEN 2080
 2100IF Q>10 THEN PRINT"Factories can't make that much this     month!":GOTO 2080
 2105 IF Q=0 THEN 2400
 2110PRINT:PRINT"You have ";Q;" new airlines."
 2115 Q=1
 2120AL=AL+Q:M=M-(Q*C)
 2125 P=P-15:IF P<0 THEN P=0
 2130IF M<0 THEN PRINT"Your advisers suggest you go easy on thespending!"
 2140 PRINT
 2150PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."
 2160 I$=GET$
 2400 RETURN
 3000 REM * * SELL A PLANE
 3010 CLS:PRINT:PRINT"SELL AN AIRCRAFT >>>"    
 3020 PRINT:PRINT"Price for used planes: 80000"
 3030 PRINT:PRINT"No. of planes now:  ";AL
 3040 PRINT:PRINT"Finance:  ";M
 3050 PRINT:PRINT:INPUT"Sell an aircraft y/n  ";X$  
 3060 IF X$="Y" OR X$="y" THEN 3070 ELSE 3400
 3070 PRINT:INPUT"How many aircraft to sell  ";X
 3080 IF X=0 THEN 3400
 3090 IF X<0 THEN GOTO 3070
 3095 PRINT
 3100 IF X>AL THEN PRINT"You don't have that many to sell!!":GOTO 3070
 3110 PRINT"You have just sold ";X;" aircraft."
 3120 AL=AL-X:M=M+(X*80000)
 3130 IF M>0 THEN PRINT"Your advisers are satisfied with your   financial position."
 3140 IF AL=0 THEN PRINT"Your advisers are worried about our lackof aircraft!"
 3150 PRINT:PRINT:COLOUR 3:PRINT"PRESS ANY KEY TO CONTINUE.."
 3160 I$=GET$
 3400 RETURN
 4000 REM * * ADVERTISE
 4010 CLS:PRINT
 4020 PRINT"ADVERTISING >>>":PRINT
 4025 IF K=1 THEN PRINT"NO MORE ADVERTISING THIS MONTH!":GOTO 4140
 4030 PRINT"FORM:            COST:":PRINT
 4040 PRINT"TV COMMERCIAL    ";TV:INPUT"Y/N  ";X$
 4050 IF X$="y" OR X$="Y" THEN M=M-300000:P=P+INT(RND(1)*8)
 4060 PRINT"RADIO COMMERCIAL ";R:INPUT"Y/N  ";X$
 4070 IF X$="Y" OR X$="y" THEN M=M-50000:P=P+INT(RND(1)*5)
 4080 PRINT"POSTER ADVERTS   ";PA:INPUT"Y/N  ";X$
 4090 IF X$="y" OR X$="Y" THEN M=M-30000:P=P+INT(RND(1)*2)
 4100 PRINT"MAGAZINE/PAPERS  ";MA:INPUT"Y/N  ";X$   
 4110 IF X$="Y" OR X$="y" THEN M=M-40000:P=P+INT(RND(1)*3)
 4120 IF P>150 THEN P=150
 4130 IF M<0 THEN PRINT"Your advisers are very concerned about  your financial position."
 4135 K=1
 4140 PRINT:PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."  
 4150 I$=GET$
 4160 RETURN
 5000 REM * * SAVE POSITION
 5010 CLS:PRINT
 5020PRINT"SAVE POSITION >>>":PRINT
 5030INPUT"Sure y/n ";X$
 5040IF X$="n" OR X$="N" THEN 5400
 5050PRINT:INPUT"ENTER FILENAME ";FL$
 5060IF FL$="" THEN 5400
 5070B=OPENOUT FL$:PRINT #B,N$,A$,MT,M,AL,Y,F,W,MC,C,FC,P,TV,R,PA,MA,K
 5080CLOSE #B
 5090PRINT"FINISHED SAVING POSITION."
 5400PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."   
 5410I$=GET$
 5420RETURN
 6000REM * * LOAD POSITION 
 6010 CLS:PRINT
 6020PRINT"LOAD POSITION >>>":PRINT
 6030INPUT"Sure y/n ";X$
 6040IF X$="n" OR X$="N" THEN 6400
 6050PRINT:INPUT"ENTER FILENAME ";FL$
 6060IF FL$="" THEN 6400
 6070B=OPENIN FL$:INPUT #B,N$,A$,MT,M,AL,Y,F,W,MC,C,FC,P,TV,R,PA,MA,K
 6080CLOSE #B
 6090PRINT"FINISHED LOADING POSITION."
 6400PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."   
 6410I$=GET$
 6420RETURN
 7000REM * * CONTINUE MONTH
 7010 CLS
 7020PRINT:PRINT"CURRENT FLIGHT PRICE: ";FC
 7030PRINT:PRINT"ENTER THIS MONTHS PRICE (PRESS RETURN   FOR NO CHANGE)"
 7040INPUT X
 7050IF X=0 THEN GOTO 7100
 7060IF X<1 THEN GOTO 7040
 7070 IF X>500 THEN PRINT"YOU CANNOT CHARGE THAT MUCH!":GOTO 7040
 7072 IF X>FC THEN P=P-INT(RND(1)*(X-FC)) 
 7073 IF (X-FC)>15 THEN P=P-INT(RND(1)*30+30)
 7074 IF X<FC THEN P=P+INT(RND(1)*6)
 7076 IF P<0 THEN P=0
 7078 IF P>150 THEN P=150
 7080FC=X
 7100 CLS:PRINT:PRINT M$(MT);" ";Y    
 7110PRINT:PRINT"No. of airliners: ";AL
 7120PRINT"Ave. no. of passengers per plane:";P:PRINT"(MAX = 150)"
 7130PRINT:PRINT:PRINT"***************************************"  
 7140PRINT"FINANCIAL INFORMATION >>>":PRINT"***************************************"
 7150 PRINT
 7155 PRINT"INCOME PER PLANE:    ";(FC*P)*30
 7160 Z=(FC*P)*30
 7165 PRINT"TOTAL INCOME:        ";Z*AL
 7170 Z=Z*AL:PRINT:F=F*AL
 7180 PRINT"TOTAL FUEL COSTS:    ";F:W=W*AL
 7190 PRINT"TOTAL WAGE COSTS:    ";W:MC=MC*AL
 7200 PRINT"TOTAL MAINTENANCE:   ";MC
 7210 E=F+W+MC
 7215 PRINT"TOTAL EXPENDITURE:   ";E
 7220 PRINT
 7225 PRINT"MONTH BALANCE:  ";:TL=Z-E:PRINT TL
 7230 PRINT
 7235 PRINT"TOTAL BALANCE:  ";:M=M+TL:PRINT M 
 7240 PRINT
 7250PRINT"PRESS ANY KEY TO CONTINUE.."
 7260I$=GET$:CLS
 7270PRINT"***************************************"
 7280PRINT"PRICE CHANGES & INCIDENTS >>>":PRINT"***************************************" 
 7285 PRINT
 7290 GOSUB 8000:IF U<5 THEN PRINT"TV ADVERTISING COSTS UP":TV=TV+15000:GOSUB 8100  
 7300GOSUB 8000:IF U<5 THEN PRINT"RADIO ADVERTISING COSTS UP":R=R+10000:GOSUB 8100 
 7310GOSUB 8000:IF U<5 THEN PRINT"MAG/PAPER ADVERT COSTS UP":MA=MA+7000:GOSUB 8100  
 7320GOSUB 8000:IF U<5 THEN PRINT"POSTER ADVERTISING COSTS UP":PA=PA+6000:GOSUB 8100 
 7330GOSUB 8000:IF U<6 THEN PRINT"COST OF NEW AIRLINERS UP":C=C+INT(RND(1)*30000):GOSUB 8100  
 7340GOSUB 8000:IF U<8 THEN PRINT"POOR WEATHER AT HOME ENCOURAGES MORE    TRAVELLERS ABROAD.":P=P+1:GOSUB 8100 
 7350GOSUB 8000:IF U<8 THEN PRINT"SPECIAL EVENTS RESULTS IN MORE PEOPLE   USING AIRLINES.":P=P+2:GOSUB 8100   
 7360GOSUB 8000:IF U<6 THEN PRINT"BOMB SCARES DISCOURAGE TRAVELLERS.":P=P-5:GOSUB 8100 
 7370GOSUB 8000:IF U<4 THEN PRINT"AN EARTHQUAKE DISCOURAGES PEOPLE GOING  TO ONE OF YOUR DESTINATIONS.":P=P-4:GOSUB 8100     
 7390GOSUB 8000:IF U<3 THEN PRINT"A RADIATION LEAK DISCOURAGES PEOPLE TO  ONE OF YOUR DESTINATIONS.":P=P-4:GOSUB 8100 
 7400GOSUB 8000:IF U<11 THEN PRINT"POOR CURRENCY EXCHANGE RATES DISCOURAGE PEOPLE TO TRAVELLING TO ONE OF YOUR     DESTINATIONS.":P=P-5:GOSUB 8100  
 7410GOSUB 8000:IF U<6 THEN PRINT"MAINTENANCE COSTS UP":MC=MC+5000:GOSUB 8100
 7420 GOSUB 8000:IF U<5 THEN PRINT"YOUR WORKERS HAVE WON A PAY RISE":W=W+15000:GOSUB 8100  
 7430 GOSUB 8000:IF U<5 THEN PRINT"FUEL COSTS HAVE GONE UP":F=F+INT(RND(1)*30000):GOSUB 8100 
 7440GOSUB 8000:IF U<10 THEN PRINT"FUEL COSTS HAVE FALLEN":F=F-INT(RND(1)*50000):GOSUB 8100  
 7442 GOSUB 8000:IF U<8 AND M>500000 THEN PRINT"THE TAXMAN MAKES A VISIT..":M=M-250000:GOSUB 8100
 7444 GOSUB 8000:IF U<5 AND M>1000000 THEN PRINT"NEW SAFETY REGULATIONS REQUIRE 700,000  ALTERATIONS TO YOUR PLANES.":M=M-700000:GOSUB 8100  
 7450 IF P<0 THEN P=0
 7460 IF P>150 THEN P=150
 7470 PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."  
 7480 I$=GET$
 7700CLS:PRINT"***************************************":PRINT"THE BANK MANAGER >>>":PRINT"***************************************"
 7710PRINT:IF M<-1000000 THEN PRINT"The bank is very unhappy with your      financial position. You are currently   1,000,000 in the RED, therefore your jobis terminated! Better luck next time..":GOTO 8200
 7720IF M<0 THEN PRINT"The bank is deeply concerned with your  financial position. Please take care."
 7730IF M>-1 AND M<500000 THEN PRINT"The bank is satisfied, but hopes for youto do better in the future."
 7740IF M>499999 THEN PRINT"The bank is pleased with your progress  and is only too happy to support you."
 7750PRINT:PRINT"PRESS ANY KEY TO CONTINUE..":I$=GET$    
 7760K=0:MT=MT+1
 7765 Q=0
 7770IF MT>12 THEN MT=1:Y=Y+1:GOSUB 8500
 7773 IF F>0 THEN F=INT(F/AL):W=INT(W/AL):MC=INT(MC/AL)    
 7780GOTO 260
 8000 U=INT(RND(1)*100):RETURN
 8100 FOR D=1 TO 500:NEXT D:PRINT:RETURN
 8200 PRINT
 8210 INPUT"Play again y/n  ";X$  
 8220 IF X$="y" OR X$="Y" THEN RUN
 8230 END
 8499 REM   END OF YEAR REVIEW
 8500 CLS:PRINT  
 8510 PRINT"***************************************":PRINT"END OF YEAR REVIEW >>>":PRINT"***************************************"
 8520 PRINT:PRINT"PLEASE WAIT A MOMENT WHILE A VERDICT IS BEING DECIDED.."
 8530 FOR D=1 TO 2500:NEXT D
 8540 PRINT
 8550 IF M>T THEN PRINT"THE COMPANY BOARD IS PLEASED THAT YOU   HAVE REACHED THE FINANCIAL TARGET FOR   THE YEAR ";Y-1;".":T1=0
 8560 IF M<T AND M>0 THEN PRINT"THE BOARD IS UNHAPPY THAT YOU HAVE NOT  ACHIEVED THE FINANCIAL TARGET SET FOR   THE LAST YEAR.":T1=T1+1 
 8570 IF M<0 THEN PRINT"THE BOARD IS VERY UNHAPPY AT THE LOSS   THE COMPANY HAS MADE IN THE LAST YEAR.":T1=T1+1
 8580 PRINT:PRINT"PLEASE WAIT WHILE THE BOARD DECIDES ON  YOUR FUTURE.."
 8590 FOR D=1 TO 2500:NEXT D
 8600 PRINT
 8610 IF T1=0 THEN PRINT"THE BOARD IS MORE THAN HAPPY TO ALLOW   YOU TO CONTINUE."
 8620 IF T1=1 THEN PRINT"THE BOARD EXPECTS MUCH MORE, BUT WILL   ALLOW YOU ANOTHER YEAR TO MAKE THE      NECESSARY IMPROVEMENTS."
 8630 IF T1=2 THEN PRINT"THE BOARD HAS NO OTHER CHOICE THAN TO   TERMINATE YOUR POSITION AFTER FAILING TOACHIEVE THE LEVELS OF SUCCESS DESIRED BYTHE DECIDING BOARD.":GOTO 8200 
 8640 PRINT
 8642 IF M<0 THEN T=5000 ELSE T=M+INT(RND(1)*T)
 8650 PRINT"THE BOARD HAS DECLAIRED YOUR TARGET FOR THE FOLLOWING YEAR IS ";T
 8660 PRINT:PRINT"PRESS ANY KEY TO CONTINUE.."
 8670 I$=GET$
 8680 RETURN
